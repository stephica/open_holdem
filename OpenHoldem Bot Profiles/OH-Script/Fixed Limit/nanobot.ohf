##2009-11-25 20:46:55##

##notes##


##dll##


##bankroll##
0.000000

##defcon##
0.500000

##rake##
0.000000

##nit##
1000

##f$alli##
0 

##f$swag##
0 

##f$srai##
0 

##f$rais##
0 
   || [  prwin >= 1.00 ]                  // the absolute nuts 
   || [  br==1 && f$br1_rais==1 ]         // round 1 raising rule 
   || [  br==2 && f$br2_rais==1 ]         // round 2 raising rule 
   || [  br==3 && f$br3_rais==1 ]         // round 3 raising rule 
   || [  br==4 && f$br4_rais==1 ]         // round 4 raising rule 

##f$call##
0 
   || [  prwin >= 1.00 ]                  // the absolute nuts 
   || [  call <= 0.00 ]                   // check! 
   || [  br==1 && f$br1_call==1 ]         // round 1 calling rule 
   || [  br==2 && f$br2_call==1 ]         // round 2 calling rule 
   || [  br==3 && f$br3_call==1 ]         // round 3 calling rule 
   || [  br==4 && f$br4_call==1 ]         // round 4 calling rule 
   || [  br==1 && f$br1_rais==1 ]         // round 1 raising rule -- backup 
   || [  br==2 && f$br2_rais==1 ]         // round 2 raising rule -- backup 
   || [  br==3 && f$br3_rais==1 ]         // round 3 raising rule -- backup 
   || [  br==4 && f$br4_rais==1 ]         // round 4 raising rule -- backup 

##f$prefold##


##f$rebuy##


##f$delay##


##f$chat##


##f$P##
(nopponentsplaying==0) ? 0 : nopponentsplaying + defcon*nopponentsfolded 

##f$play##
//------------------------------------------------------------------------------- 
//    DISABLED RULE(S) 
!1                       ? -1 :     // 0=disabled 1=enabled 
!f9                      ? -1 :     // enabled when f9 is pressed 
(elapsed%4)              ? -1 :     // disabled 3 out of 4 seconds 

//------------------------------------------------------------------------------- 
//    LEAVE RULE 
1                                   // 0=off 1=on 
&& issittingout                     // i am sitting out 
&& elapsedauto >= 600               // 10 minutes since autoplayer has acted 
                         ? -2 :     // leave the table 
//------------------------------------------------------------------------------- 
//    SITIN RULE 
1                                   // 0=off 1=on 
&& issittingout                     // i am sitting out 
&& br==2                            // it is the flop 
&& nopponentsdealt>=3               // 3 or more opponents 
                         ?  1 :     // sitin 
//------------------------------------------------------------------------------- 
//    SITOUT RULE 
1                                   // 0=off 1=on 
&& issittingin                      // i am sitting in 
&& (br==2 || br==3)                 // it is the flop or turn 
&& nopponentsdealt<=2               // 2 or less opponents 
                      ?  0 :        // sitout 
//------------------------------------------------------------------------------- 
-1 // disabled 

##f$test##


##f$debug##


##list0##


##list1##


##list2##


##list3##


##list4##


##list5##


##list6##


##list7##


##f$evrais##
0 

##f$evcall##
0 

##f$safeboard##
0 
   || [  nrankedcommon == 1                            // board not paired 
      && nsuitedcommon <=2                             // board not three to a flush 
      && nstraightcommon <= 2 ]                        // board not three to a straight 

##f$br1_rais##
0 
   || [  handrank169 <= 4 ] 
   || [  handrank169 <= 169*(dealposition/nplayersdealt)*0.125 ] 
   || [  handrank169 <= 169*(dealposition/nplayersdealt)*0.200 && ncallbets <=1 ] 

   || [  dealposition == nplayersdealt 
      && nopponentsplaying == 2 
      && pot==(bblind+sblind) 
      && handrank169 <= 90.00 ] 

   || [  dealposition == 1 
      && nopponentsplaying == 1 
      && pot==(bblind+sblind) 
      && handrank169 <= 90.00 ] 

   || [  dealposition == 2 
      && nopponentsplaying == 1 
      && pot==(bblind*2) 
      && handrank169 <= 90.00 ] 

##f$br1_call##
0 
   || [ didrais ] 
   || [ didcall && nbetstocall <= 1 ] 
   || [  handrank169 <= 169*(dealposition/nplayersdealt)*0.250 && ncallbets <= 2 ] 
   || [  handrank169 <= 169*(dealposition/nplayersdealt)*0.450 && ncallbets <= 1 ] 

##f$br2_rais##
0 
// ########## STRAIGHT DRAW ########## 
// straight draw 
   || [  nstraight == 4                                // i have a straight draw 
      && ncallbets <= 1 ]                              // pot is not raised 

// ########## FLUSH DRAW ########## 
   || [  nsuited == 4                                  // i have a straight draw 
      && nsuitedcommon <= 2                            // board is not three to a flush 
      && ncallbets <= 1 ]                              // pot is not raised 

// ########## ONE PAIR ########## 
// one pair, high pair 
   || [  ishipair                                      // i have a high pair 
      && nrankedcommon == 1                            // board is not paired 
      && !didrais ]                                    // i didn't raise this round 

// one pair, middle pair 
   || [  ismidpair                                     // i have a middle pair 
      && nrankedcommon == 1                            // board is not paired 
      && ncallbets <= 1 ]                              // pot is not raised 

// one pair, middle pair, raised preflop 
   || [  ismidpair                                     // i have a middle pair 
      && nrankedcommon == 1                            // board is not paired 
      && ncallbets <= 1                                // pot is not raised 
      && didraisround1 == 1 ]                          // raised preflop - show strength 

// ########## TWO PAIR ########## 
// two pair 
   || [  istwopair && !didrais ]                       // i didn't raise this round 

// ########## THREE OF A KIND ########## 
   || [ isthreeofakind                                 // i have three of a kind 
      && nrankedcommon <= 2                            // board is not three of a kind 
      && !didrais ]                                    // i didn't raise this round 

// ########## MADE HANDS ########## 
   || [  isstraight ]                                  // i have a straight 
   || [  isflush ]                                     // i have a flush 
   || [  isfullhouse ]                                 // i have a full house 
   || [  isfourofakind ]                               // i have four of a kind 
   || [  isstraightflush ]                             // i have a straight flush 
   || [  isroyalflush ]                                // i have a royal flush 

##f$br2_call##
0 
   || [ didrais ]                                      // i raised this round 

// ########## STRAIGHT DRAW ########## 
// straight draw 
   || [  nstraight >= 4                                // i have a straight draw 
      && ncallbets <= 1 ]                              // pot is not raised 

// ########## FLUSH DRAW ########## 
   || [  nsuited == 4                                  // i have a flush draw 
      && nsuitedcommon <= 2                            // board is not three to a flush 
      && ncallbets <= 1 ]                              // pot is not raised 

// ########## HIGH CARD ########## 
// hi card, raised preflop, safe board 
   || [  $AX                                           // i have AXx 
      && ishicard == 1                                 // high card 
      && f$safeboard == 1                              // board is safe 
      && ncallbets <= 1 ]                              // pot is not raised 

// ########## ONE PAIR ########## 
// one pair, high pair 
   || [  ishipair == 1                                 // i have a high pair 
      && nrankedcommon == 1                            // board is not paired 
      && ncallbets <= 2 ]                              // pot is not re-raised 

// one pair, not high pair, did PFR 
   || [  (ismidpair == 1 || islopair == 1)             // i have a pair not highpair 
      && nrankedcommon == 1                            // board is not paired 
      && ncallbets <= 1                                // pot is not raised 
      && didraisround1 == 1 ]                          // raised preflop - show strength 

// one pair, not high pair 
   || [  (ismidpair == 1 || islopair == 1)             // i have a pair not highpair 
      && nrankedcommon == 1                            // board is not paired 
      && ncallbets <= 1 ]                              // pot is not raised 

// ########## MADE HANDS ########## 
   || [  istwopair ]                                   // i have two pair 
   || [  isthreeofakind ]                              // i have three of a kind 
   || [  isstraight ]                                  // i have a straight 
   || [  isflush ]                                     // i have a flush 
   || [  isfullhouse ]                                 // i have a full house 
   || [  isfourofakind ]                               // i have four of a kind 
   || [  isstraightflush ]                             // i have a straight flush 
   || [  isroyalflush ]                                // i have a royal flush 

##f$br3_rais##
0 
// ########## PURE BLUFF ########## 
   || [ didchecround2 == 1                             // we checked at the flop 
      && ncallbets == 0                                // it's checked to me 
      && betposition == nplayersplaying ]              // i am last to act 

// ########## STRAIGHT DRAW ########## 
// straight draw 
   || [  nstraight == 4                                // i have a straight draw 
      && ncallbets <= 0 ]                              // pot is not bet 

// ########## FLUSH DRAW ########## 
   || [  nsuited == 4                                  // i have a flush draw 
      && nsuitedcommon <= 3                            // board is not three to a flush 
      && ncallbets <= 0 ]                              // pot is not bet 

// ########## ONE PAIR ########## 
// one pair, high pair, safe board 
   || [  ishipair == 1                                 // i have a high pair 
      && nrankedcommon == 1                            // board is not paired 
      && !didrais ]                                    // i didn't raise this round 

// ########## TWO PAIR ########## 
   || [  istwopair == 1                                // i have two pair 
      && nrankedcommon == 1                            // board is not paired 
      && !didrais ]                                    // i didn't raise this round 

// ########## MADE HANDS ########## 
   || [  isthreeofakind && !didrais ]                  // i have three of a kind 
   || [  isstraight && !didrais ]                      // i have a straight 
   || [  isflush && !didrais ]                         // i have a flush 
   || [  ishiflush ]                                   // i have a high flush 
   || [  isfullhouse ]                                 // i have a full house 
   || [  isfourofakind ]                               // i have four of a kind 
   || [  isstraightflush ]                             // i have a straight flush 
   || [  isroyalflush ]                                // i have a royal flush 

##f$br3_call##
0 
   || [ didrais ]                                      // i raised this round 

// ########## STRAIGHT DRAW ########## 
// straight draw 
   || [  nstraight == 4                                // i have a straight draw 
      && ncallbets <= 1 ]                              // pot is not raised 

// ########## FLUSH DRAW ########## 
   || [  nsuited == 4                                  // i have a flush draw 
      && nsuitedcommon <= 3                            // board is not three to a flush 
      && ncallbets <= 1 ]                              // pot is not raised 

// ########## ONE PAIR ########## 
// one pair, high pair, safe board 
   || [  ishipair == 1                                 // i have a high pair 
      && f$safeboard == 1 ]                            // board is safe 

// one pair, high pair, with straight- or flushdraw 
   || [  ishipair == 1                                 // i have a high pair 
      && ncallbets <= 1 ]                              // pot is not raised 

// one pair, mid pair, safe board 
   || [  ismidpair == 1                                // i have a mid pair 
      && f$safeboard == 1                              // board is safe 
      && ncallbets <= 2 ]                              // pot is not re-raised 

// ########## MADE HANDS ########## 
   || [  istwopair ]                                   // i have two pair 
   || [  isthreeofakind ]                              // i have three of a kind 
   || [  isstraight ]                                  // i have a straight 
   || [  isflush ]                                     // i have a flush 
   || [  isfullhouse ]                                 // i have a full house 
   || [  isfourofakind ]                               // i have four of a kind 
   || [  isstraightflush ]                             // i have a straight flush 
   || [  isroyalflush ]                                // i have a royal flush 

##f$br4_rais##
0 
// ########## ONE PAIR ########## 
// one pair, high pair 
   || [  ishipair == 1                                 // i have a high pair 
      && nstraightcommon <= 3                          // board not four to a straight 
      && nsuitedcommon <= 3                            // board not four to a flush 
      && ncallbets <= 0                                // pot is not bet 
      && !didrais ]                                    // i didn't raise this round 

// ########## TWO PAIR ########## 
   || [  istwopair == 1                                // i have two pair 
      && nrankedcommon == 1                            // board is not paired 
      && nstraightcommon <= 3                          // board not four to a straight 
      && nsuitedcommon <= 3                            // board not four to a flush 
      && ncallbets <= 0                                // pot is not bet 
      && !didrais ]                                    // i didn't raise this round 

// ########## THREE OF A KIND ########## 
// three of a kind, safe board 
   || [ isthreeofakind                                 // i have three of a kind 
      && f$safeboard == 1                              // board is safe 
      && !didrais ]                                    // i didn't raise this round 

// three of a kind, with flushdraw 
   || [  isthreeofakind                                // i have three of a kind 
      && !(pokervalcommon & threeofakind)              // board is not three of a kind 
      && nsuitedcommon >= 3                            // board three or more to a flush 
      && ncallbets == 0                                // pot is not bet 
      && !didrais ]                                    // i didn't raise this round 

// three of a kind, with straightdraw 
   || [  isthreeofakind                                // i have three of a kind 
      && !(pokervalcommon & threeofakind)              // board is not three of a kind 
      && nstraightcommon >= 3                          // board three or more to a straight 
      && ncallbets == 0                                // pot is not bet 
      && !didrais ]                                    // i didn't raise this round 

// ########## STRAIGHT ########## 
// high straight, no flush possible 
   || [  ishistraight                                  // i have highest straight 
      && nsuitedcommon <= 2 ]                          // board not three to a flush 

// middle straight, no flush possible 
   || [  isstraight                                    // i have a straight 
      && !ishistraight                                 // i don't have the highest straight 
      && nsuitedcommon <= 2                            // board not three to a flush 
      && ncallbets <= 1 ]                              // pot is not raised 

// ########## FLUSH ########## 
// middle flush 
   || [  isflush                                       // i have a flush 
      && ncallbets == 0 ]                              // pot is not bet 

// ########## MADE HANDS ########## 
   || [  ishiflush ]                                   // i have highest flush 
   || [  isfullhouse ]                                 // i have a full house 
   || [  isfourofakind ]                               // i have four of a kind 
   || [  isstraightflush ]                             // i have a straight flush 
   || [  isroyalflush ]                                // i have a royal flush 

##f$br4_call##
0 
   || [ didrais ]                                      // i raised this round 

// ########## ONE PAIR ########## 
// one pair, high pair, safe board 
   || [  ishipair == 1                                 // i have a high pair 
      && nrankedcommon == 1                            // board is not paired 
      && nsuitedcommon <=3                             // board not four to a flush 
      && nstraightcommon <= 3                          // board not four to a straight 
      && ncallbets <= 1 ]                              // pot is not raised 

// one pair, middle pair, safe board 
   || [  ismidpair == 1                                // i have a mid pair 
      && nrankedcommon == 1                            // board is not paired 
      && nsuitedcommon <=3                             // board not four to a flush 
      && nstraightcommon <= 3                          // board not four to a straight 
      && ncallbets <= 1 ]                              // pot is not raised 

// ########## MADE HANDS ########## 
   || [  istwopair ]                                   // i have two pair 
   || [  isthreeofakind ]                              // i have three of a kind 
   || [  isstraight ]                                  // i have a straight 
   || [  isflush ]                                     // i have a flush 
   || [  isfullhouse ]                                 // i have a full house 
   || [  isfourofakind ]                               // i have four of a kind 
   || [  isstraightflush ]                             // i have a straight flush 
   || [  isroyalflush ]                                // i have a royal flush


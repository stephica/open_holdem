##2009-11-25 20:47:08##

##notes##
Playbot3. Created by Matrix as an example of a prwin, 
pot odds bot with the following bells and whistles:
Bet and call capping to limit pot chasing in marginal situations
EV's modified by proportion of bulls, bears and limpers 
Autobet and Check Raise logic 
Options for random trimming of decision levels to reduce predictability 
Plenty of constants to trim to get the bot to play your way. 

EDITED 2005-10-30 to correct EV criteria and handrank oddity. 

EDITED 2009-05-31 by TheHighFish: 
  * conversion to new ohf-format 
  * comments for unused standard formulae 
  * put some symbols into the debug-tab to demonstrate its usage 
  * removed unused hand-lists

##dll##


##bankroll##
0.000000

##defcon##
0.000000

##rake##
0.000000

##nit##
1000

##f$alli##
   0                                     // default is do not alli 
   || [ prwin >= 1.00 ]                  // dead nuts

##f$swag##
// No stated wagers with a FL-bot.

##f$srai##
// No stated raises with a FL-bot.

##f$rais##
   0 
   ||    [ prwin >= 1.00 ]                  // dead nuts 

   || br==1 
//round 1 logic 
   ?
//bet-capping logic. 
//We initially used some random modifications to prevent the bot being profiled too accurately 
//prwin was modified by at max + or - 0.1 
//This is of questionable value, and made the bot difficult to tune 
//e,g.   [(ncurrentbets==1)&&(prwin+((5-(10*randomround))/50)>0.4)] || 
   ( 
   [(ncurrentbets==3)&&(prwin>0.7)] ||  //change the >constants to taste 
   [(ncurrentbets==2)&&(prwin>0.55)] || 
   [(ncurrentbets==1)&&(prwin>0.4)] || //should be greater than have acted value 
   [ncurrentbets<1] 
   ) 
//end of bet-capping logic 

   &&

//this determines if we want to raise at all pre-flop 
   ( 
   ncurrentbets<1 
   ? 
//have not acted yet logic. Separate because prwin will probably increase through folds on first circuit 
   ( 
   handrank1000 < (300 - 20*nopponentsplaying)  //trim constants to taste 
   ) 
//end of have not acted yet logic 
   : 
//have acted logic 
   ( 
   prwin > 0.3 // constant should not be greater than the minimum possible prwin value which 
                  // can match the ncurrentbets==1 capping value 
   ) 
//end of have acted logic 
   ) 
// end of preflop raise logic 
   : 
//post-flop logic 
   ( 
//bet-capping logic. 
   ( 
   [(ncurrentbets==3)&&(prwin>0.8)] || //change >constants to taste 
   [(ncurrentbets==2)&&(prwin>0.6)] || 
   [(ncurrentbets==1)&&(prwin>0.5)] || 
   [ncurrentbets<1] || [ncurrentbets>=4] 
   ) 
//end of bet capping logic 
   && 
//check raise code - comment out active line if not wanted 
   !( 
   !ncurrentbets && [randomround > 0.75] && [prwin > 0.5] //adjust constants to taste, 0.75 = 1 in 4 
   ) 
//end of check raise code 
   && 
//postflop raise logic 
   ( 
   0 
   || [ f$evrais>f$evcall && f$evrais>(pot/3) ]  // guesswork 
   || 
//autobet logic - comment out active line if not wanted 
   ( 
   br==2 && didraisround1 && !nbetstocall 
   && [randomround > 0.5] && [prwin > 0.3] //adjust constants to taste 
   ) 
//end of autobet logic 
   ) 
//end of postflop raise logic 
   ) 
//end of f$rais

##f$call##
   0                                     // default is do not call 
   || [ prwin >= 1.00 ]                  // dead nuts 
   || [ call  <= 0.00 ]                  // zero call amount (check) 
   || br==1 
//round 1 logic 
   ? 
//bet-capping logic. 
   ( 
   [handrank169 < 12] ||  //guard against a few top hands being wasted by prwin on a full table 
   [(ncurrentbets==3)&&(prwin>0.3)] ||  //change the >constants to taste 
   [(ncurrentbets==2)&&(prwin>0.25)] || 
   [(ncurrentbets==1)&&(prwin>0.2)] || //should be greater than have acted value 
   [ncurrentbets<1] || [ncurrentbets==4] 
   ) 
//end of bet-capping logic 

   && 

//this determines if we want to call at all pre-flop 
   ( 
   ncurrentbets<1 
   ? 
//have not acted yet logic. Separate because prwin will probably increase through folds on first circuit 
   ( 
   handrank1000 < (400 - 10*nopponentsplaying)  //trim constants to taste 
//trim constants to taste 
   ) 
//end of have not acted yet logic 
   : 
//have acted logic 
   ( 
   prwin > 0.15 // constant should not be greater than the minimum possible prwin value which 
                  // can match the ncurrentbets==1 capping value 
   ) 
//end of have acted logic 
   ) 
// end of preflop call logic 
   : 
//post-flop logic 
   ( 
//bet-capping logic. 
   ( 
   [(ncurrentbets==3)&&(prwin>0.5)] || //change >constants to taste 
   [(ncurrentbets==2)&&(prwin>0.4)] || 
   [(ncurrentbets==1)&&(prwin>0.3)] || 
   [ncurrentbets<1] || [ncurrentbets>=4] 
   ) 
//end of bet capping logic 
   && 
//postflop call logic 
   ( 
   0 
   || [ f$evcall>pot/(nplayersdealt+2) ]  // guesswork 
   ) 
//end of postflop calle logic 
   ) 
//end of f$call

##f$prefold##
// Don't prefold anything 
0

##f$rebuy##
// <=0 means: don't rebuy.
//  >0 means: target-amount.
// Default is: no rebuy.
0

##f$delay##
// No delay to act. 
0

##f$chat##
// No chat. 
0

##f$P##
// Number of opponents for the prwin simulation.

(nopponentsplaying==0) ? 0 : 
nopponentsplaying + defcon*nopponentsfolded

##f$play##
//------------------------------------------------------------------------ 
//    DISABLED RULE(S) 
!1                       ? -1 :  // 0=disabled 1=enabled 
!f9                      ? -1 :  // enabled when f9 is pressed 
//(elapsed%4)              ? -1 :  // disabled 3 out of 4 seconds 


//------------------------------------------------------------------------ 
//    LEAVE RULE 
0                                // 0=off 1=on 
&& issittingout                  // i am sitting out 
&& elapsedhand >= 600            // 10 minutes since end of last hand 
&& elapsedauto >= 600            // 10 minutes since autoplayer has acted 

                         ? -2 :  // leave the table 

//------------------------------------------------------------------------ 
//    SITIN RULE 

0                                // 0=off 1=on 
&& issittingout                  // i am sitting out 
//&& (br==2 || br==3)              // it is the flop or turn 
&& (nopponentsdealt >= 2)             // 6 or more opponents 
//&& nchairsdealtright==2          // i am about to be big blind 

                         ?  1 :  // sitin 

//------------------------------------------------------------------------ 
//    SITOUT RULE 
1                                // 0=off 1=on 
&& issittingin                   // i am sitting in 
&& (br==2 || br==3)              // it is the flop or turn 
&& (nopponentsdealt <= 1)             // 5 or less opponents 

                         ?  0 :  // sitout 

//------------------------------------------------------------------------ 
-1 // disabled

##f$test##


##f$debug##
                 = elapsed

                 = sblind
                 = bblind
                 = bet

                 = call
                 = currentbet
                 = balance

                 = f$evcall
                 = f$evrais

                 = potplayer
                 = potcommon
                 = pot
                 = rake

                 = chair
                 = handrank2652
                 = pokerval
                 = prwin
                 = prlos
                 = prtie
                 = nopponents

##f$evrais##
0                                        // start with zero 

// DEBITS HERE 

- call                                   // calling cost 
- bet                                    // raising cost 

// CREDITS HERE 

+  ( 

      pot                                // current pot 

   + (call+bet*2)                        // my call my bet and a call (minimum) 

   + (raisshort-call-bet*2)              // additional callers 
      * 0.00                             //   0% (tight) 
//    * 0.50                             //  50% (guess) 
//    * 1.00                             // 100% (loose) 

   ) 

   * (prwin+prtie/2)                     // adjust for winning chances 
   * (1+rake)                            // adjust for rake 
   * (1-((nopponentsraising/nopponentsplaying)/3)) //adjust constant for aggressors 
   * (1+((nopponentschecking/nopponentsplaying)/5)) //adjust constant for lurkers 
   * (1-((nopponentscalling/nopponentsplaying)/10)) //adjust constant for limpers

##f$evcall##
0                                        // start with zero 

// DEBITS HERE 

- call                                   // calling cost 


// CREDITS HERE 

+  ( 

      pot                                // current pot 

   +  call                               // my call (minimum) 

   + (callshort-call)                    // additional callers 
      //* 0.00                             //   0% (tight) 
      * 0.50                             //  50% (guess) 
//    * 1.00                             // 100% (loose) 

   ) 

   * (prwin+prtie/2)                     // adjust for winning chances 
   * (1+rake)                            // adjust for rake 
   * (1-((nopponentsraising/nopponentsplaying)/4)) //adjust constant for aggressors 
   * (1+((nopponentschecking/nopponentsplaying)/10)) //adjust constant for lurkers 
   * (1-((nopponentscalling/nopponentsplaying)/20)) //adjust constant for waiters 


##2009-11-25 20:45:10##

##notes##
Sit N Go Starter Bot

##dll##


##bankroll##
0.000000

##defcon##
0.000000

##rake##
0.000000

##nit##
10000

##f$alli##
   0        // default is do not alli 

// If Betting go Allin if less than 10 big blinds
   || [br==1 && f$srai && balance!=0 && balance<=10*bblind] 

// Allin Great Starting Hands
   || [br==1 && islist1 && didswag]
   || [br==1 && islist1 && ncallbets>1]
   || [br==1 && islist2 && ncallbets>1 && nopponentsdealt<=5]

//Allin by Stack size
   || [br==1 && islist2 && ncallbets>1 && balance<15*bblind]
   || [br==1 && islist3 && balance<10*bblind]
   || [br==1 && islist4 && balance<8*bblind]
   || [br==1 && islist5 && balance<6*bblind]
   || [br==1 && ($Kx || $Qx || $Jx) && balance<4*bblind]
   || [balance<2*bblind && balance!=0] 
 
 // Pre Flop Odds All INS  
   || [br==1 && didswag && balance<6*currentbet && islist2]
   || [br==1 && didswag && balance<5*currentbet && islist3]
   || [br==1 && didswag && balance<4*currentbet && islist4]
   || [br==1 && didswag && balance<3*currentbet && islist5]
   || [br==1 && didswag && balance<2*currentbet]

// ITM
   || [br==1 && nopponentsseated==2 && islist3 && ncallbets>1 && balance!=0]
   || [br==1 && nopponentsseated==1 && islist5 && ncallbets>1 && balance!=0] 
   || [br==1 && nopponentsseated<=2 && didswag && balance<3*currentbet && balance!=0]
  
//Post Flop Allin
   || [br==2 && didswaground1 && nopponentsbetting==0 && nplayersround2<=3 && balance<pot]
   || [br==2 && didswag && prwinnow>.90 && balance<2*currentbet && balance!=0]   
   || [br>=2 && prwinnow>.90 && balance<12*bblind && balance!=0] 
   || [br>=2 && prwinnow>.85 && balance<8*bblind && balance!=0] 
   || [br>=2 && prwinnow>.75 && balance<6*bblind && balance!=0]
   || [br>=2 && prwinnow>.70 && balance<5*bblind && balance!=0]
   || [br>=2 && islist5 && balance<4*bblind && balance!=0]
   || [br>=2 && didswag && balance<=400 && balance!=0]   
   || [br>=2 && prwinnow>=.95]
   || [br==3 && didswaground2 && balance<5*bblind && balance!=0]

##f$swag##
f$srai>=f$sraimin 

? ((((f$swag_adjust+f$srai)/sblind)+.5)^0)*sblind : 

0

##f$srai##
(br==1 && islist2 && ncallbets==1) ? (bblind+(nopponentsbetting*bblind)) :
(br==1 && islist3 && bblind>=100 && ncallbets==1) ? (bblind+(nopponentsbetting*bblind)) : 
(br==1 && islist3 && bblind>=200 && ncallbets==1) ? (bblind+(nopponentsbetting*bblind)) :
(br==1 && islist2 && bblind>=100 && ncallbets>1  && ncallbets<6) ? pot:
(br==1 && islist1 && ncallbets>1) ? 1.5*pot:

//Steal
(br==1 && prwin>.30 && bblind>=100 && (pot<=(sblind+bblind)) && ((betposition/nplayersplaying)>.6)) ? (bblind+(nopponentsbetting*bblind)) : 
(br==1 && prwin>.35 && bblind>=100 && (pot<=(sblind+bblind)) && ((betposition/nplayersplaying)>.9)) ? (bblind+(nopponentsbetting*bblind)) : 
(br==1 && prwin>.45 && bblind>=100 && (pot<=(sblind+bblind)) && dealposition==1) ? (bblind+(nopponentsbetting*bblind)) : 

//Continuation Bet
(br==2 && (didswaground1&&!didcallround1) && nopponentsbetting==0 && nplayersround2<=3) ? pot*.65 :
(br==2 && didcallround1 && nopponentsbetting==0 && nplayersround2<=3 && ((betposition/nplayersplaying)==1)) ? pot*.65 :

// Bet a hand
(br==2 && prwinnow>.70 && nopponentsbetting==0 && ((betposition/nplayersplaying)>.5)) ? pot*.65 :
(br==2 && prwinnow>.80 && nopponentsbetting==0) ? pot*.65 :
(br==3 && pokerval>pokervalcommon && ncallbets==0 && ((betposition/nplayersplaying)>.5) && prwinnow>.80 && nbetsround2==0) ? pot*.65 : 
(br==4 && pokerval>pokervalcommon && ncallbets==0 && ((betposition/nplayersplaying)>.5) && (prwin+prtie)>.80 && nbetsround3==0) ? pot*.65 : 
(br>=3 && ncallbets>=0 && currentbet==0 && prwinnow>=.90) ? pot*.50 :
(br>=3 && ncallbets>0 && prwinnow>=.95) ? pot : 

//Bet a draw
(br==2 && nsuited==4 && nsuitedcommon<3 && nrankedcommon<2 && ((betposition/nplayersplaying)==1) && ncallbets==0) ? pot*.65 :
(br==2 && nstraight==4 && !(rankbits&16386 && rankbits&8196) && nrankedcommon<2 && nsuitedcommon<2 && ((betposition/nplayersplaying)==1) && ncallbets==0) ? pot*.65 : 

//ITM
(br==1 && ncallbets==1 && nopponentsseated==1 && prwin>=.40 && currentbet<bblind) ? 3*bblind :
(br==1 && ncallbets==1 && nopponentsseated==1 && prwin>=.40 && currentbet==bblind) ? 4*bblind :
(br==1 && ncallbets<=2 && nopponentsseated==1 && prwin>=.70 && currentbet==bblind) ? pot :
(br>=2 && nopponentsseated<=2 && ncallbets==0 && prwinnow>.65) ? pot*.65 : 
(br>=2 && nopponentsseated<=2 && ncallbets>=1 && prwinnow>.80) ? pot : 0

##f$rais##
0                                        // default is do not rais
   || [ f$alli]
   || [ f$srai]

##f$call##
0 
   || [ prwin >= .98 ]                  
   || [ call<=0] 
   || [ f$alli]
   || [ f$srai]

// Limp Early Rounds
   || [ br==1 && islist4 && call<=1*bblind && bblind<100 && nopponentsbetting>3]
   || [ br==1 && ispair  && call<=3*bblind && bblind<100 && nopponentsbetting>3]
   || [ br==1 && islist2 && call<=5*bblind]
   || [ br==1 && islist2 && call<=150]
    
// Bet or Called and small bet left to Call
   || [ (didswag||didcall) && call<=bblind]

   || [ br==2 && didswaground1 && call<=2*bblind]
   || [ br==3 && didswaground2 && call<=1*bblind]

// Calls 
   || [ br>=2 && call<=1*bblind  && prwinnow>=.60]
   || [ br>=2 && call<=2*bblind  && prwinnow>=.70]
   || [ br>=2 && call<=3*bblind  && prwinnow>=.80]
   || [ br>=2 && call<=4*bblind  && prwinnow>=.85]
   || [ br>=2 && call<=7*bblind  && prwinnow>=.90]
   || [ br>=2 && call<=500 && prwinnow>=.90]

// Draws
   || [ br==2 && nsuited==4 && nsuitedcommon<3 && nrankedcommon==1 && pot>=3*call]
   || [ br==2 && nstraight==4 && !(rankbits&16386 && rankbits&8196) && nrankedcommon==1 && nsuitedcommon<2 && pot>=3*call]

##f$prefold##


##f$rebuy##


##f$delay##


##f$chat##


##f$P##

(nopponentsplaying==0) ? 0 :

nopponentsplaying + defcon*nopponentsfolded

##f$play##

-1 // disabled

##f$test##


##f$debug##


##list1##
AA  KK  

##list2##
AA  KK  QQ  JJ  
AKs 
KA  

##list3##
AA  KK  QQ  JJ  TT  99  88  
AKs AQs AJs KQs 
KA  QA  

##list4##
AA  KK  QQ  JJ  TT  99  88  77  66  55  44  33  22  
AKs AQs AJs ATs A9s A8s A7s A6s A5s A4s A3s A2s KQs KJs KTs QJs QTs JTs 
KA  QA  QK  JA  JK  TA  

##list5##
AA  KK  QQ  JJ  TT  99  88  77  66  55  44  33  22  
AKs AQs AJs ATs A9s A8s A7s A6s A5s A4s A3s A2s KQs KJs KTs QJs QTs JTs 
KA  QA  QK  JA  JK  JQ  TA  TK  TQ  TJ  9A  8A  7A  6A  5A  4A  3A  2A  

##f$evrais##
0

##f$evcall##
0

##f$sraimin##
[currentbet+call]

##f$swag_adjust##
[swagtextmethod == 3 ? (call + currentbet) : 
 swagtextmethod == 2 ? (call) : 0]


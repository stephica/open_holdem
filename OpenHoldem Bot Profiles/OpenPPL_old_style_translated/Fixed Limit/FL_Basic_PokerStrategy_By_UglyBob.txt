// Contributed by UglyBob, Tue Feb 28, 2012
// http://www.maxinmontreal.com/forums/viewtopic.php?f=260&t=14747


// uglybob's PokerStrategy.com Basic Fixed Limit - Texas Hold'em Bot.
// v1.1 alpha 02-25-2012
//
// This bot was developed to follow the basic FL strategy from the site PokerStrategy.com.
// It's contains some little and commenteds strategic changes.
// 90% is the original strategy.
// (I do not know if the code is really trusty to the rule because it was not yet tested, it's an alpha version.)
// Tests coming soon. :)
//
// uglybob@hush.ai
//

CUSTOM

LISTS

	NEW LIST 001 
		// Really strong hands.
		AA KK QQ AKs AKo
	END LIST

	NEW LIST 002
		// Strong hands.
		JJ TT 99 AQs AQo AJs
	END LIST

	NEW LIST 003
		// Mediocre hands.
		AJo ATs ATo KQs KQo
	END LIST

	NEW LIST 004
		// Speculative hands.
		22 33 44 55 66 77 88 KJs KTs QJs QTs JTs T9s
	END LIST

	NEW LIST 005
		// Mixed hands.
		KJo KTo QJo QTo JTo A2s A3s A4s A5s A6s A7s A8s A9s K9s 87s 98s
	END LIST

PREFLOP

	// In preflop the betsize is 1 small bet. (1 small bet = 1 big blind)

	// This bot don't care about the number of players at the table.
	// I guess that the minimum is 7, but not sure.

	// ------------------------------------------------------------------------------------ //
	WHEN ((islist001)) RAISE FORCE // Really strong hands.
	// Nothing matter, with a hand like that in preflop you just raise everytime.
	// ------------------------------------------------------------------------------------ //

	// ------------------------------------------------------------------------------------ //
	// Strong hands.

	WHEN ((islist002) AND (BotsActionsOnThisRoundIncludingChecks > 0)) CALL FORCE
	// After me two or more players raises, call.
	// With this hand you just call if you need play again in preflop.

	WHEN ((islist002) AND (Raises > 1)) FOLD FORCE 
	// Two or more raises before I play, fold.

	WHEN ((islist002) AND (Raises = 0)) RAISE FORCE
	// No raises before I play, raise.

	WHEN ((islist002) AND (Raises = 1) AND (CallsSinceLastRaise < 1))
		// One raises and no calls, check my position:
		WHEN (NOT (InEarlyPosition1) AND NOT (InEarlyPosition2) AND NOT (InEarlyPosition3)) RAISE FORCE
		WHEN (Others) FOLD FORCE
		// If I am not in a early position, raise.
		// Otherwise fold.

	WHEN ((islist002) AND (Raises = 1) AND (CallsSinceLastRaise > 0)) CALL FORCE
	// One raise and at least one player calls, call.

	// ------------------------------------------------------------------------------------ //

	// ------------------------------------------------------------------------------------ //
	// Mediocre hands.

	WHEN ((islist003) AND ((InEarlyPosition1) OR (InEarlyPosition2) OR (InEarlyPosition3))) FOLD FORCE
	// Early positions with a hand like that: fold.

	WHEN ((islist003) AND (BotsActionsOnThisRoundIncludingChecks > 0) AND (RaisesSinceLastPlay > 1)) FOLD FORCE
	// More than one raise after me, fold.
	
	WHEN ((islist003) AND (BotsActionsOnThisRoundIncludingChecks > 0) AND (RaisesSinceLastPlay = 1)) CALL FORCE
	// Only one raise after me, call.
	
	WHEN ((islist003) AND (Raises > 1)) FOLD FORCE
	// More than one raise before I play, fold.

	WHEN ((islist003) AND (Raises = 0)) RAISE FORCE
	// No raises? Raise.

	WHEN ((islist003) AND (Raises = 1) AND (CallsSinceLastRaise < 1) AND (InBigBlind)) CALL FORCE
	// One raise, no calls and I'm the big blind, call.

	WHEN ((islist003) AND (Raises = 1) AND (CallsSinceLastRaise > 0) AND ((InBigBlind) OR (Hand = KQ suited))) CALL FORCE
	// One raise, at least one call and I'm the big blind or have KQs in hand, call.

	// ------------------------------------------------------------------------------------ //

	// ------------------------------------------------------------------------------------ //
	// Speculative hands.

	WHEN ((islist004) AND (BotsActionsOnThisRoundIncludingChecks > 0) AND (RaisesSinceLastPlay > 1)) FOLD FORCE
	// More than one raise after me, fold.

	WHEN ((islist004) AND (BotsActionsOnThisRoundIncludingChecks > 0) AND (RaisesSinceLastPlay = 1)) CALL FORCE
	// Only one raise after me, call.

	WHEN ((islist004) AND (Raises > 1)) FOLD FORCE
	// More than one raise before I play, fold.

	WHEN ((islist004) AND (Raises = 1) AND (CallsSinceLastRaise > 0)) CALL FORCE
	// One raise, at least one call, call.

	WHEN ((islist004) AND (Raises = 1) AND (CallsSinceLastRaise < 1) AND (InBigBlind)) CALL FORCE
	// One raise, no calls and I'm the big blind, call.

	WHEN ((islist004) AND (Raises = 0) AND (Calls > 1)) CALL FORCE
	// No raises, at least 2 calls, call. (Or check if it's possible)

	WHEN ((islist004) AND (Raises = 0) AND (Calls = 1))
		// No raises, one call, check my position:
		WHEN ((InBigBlind)) CALL FORCE		
		WHEN ((InEarlyPosition1) OR (InEarlyPosition2) OR (InEarlyPosition3)) FOLD FORCE
		WHEN ((InMiddlePosition1) OR (InMiddlePosition2) OR (InMiddlePosition3)) FOLD FORCE		
		WHEN (Others) CALL FORCE
		// Big blind gonna call (check). (Backup Actions)
		// Early or middle positions, fold.
		// Others positions, call.

	WHEN ((islist004) AND (Raises = 0) AND (Calls = 0))
		// No raises, no call, check my position:
		WHEN ((InEarlyPosition1) OR (InEarlyPosition2) OR (InEarlyPosition3)) FOLD FORCE
		WHEN ((InMiddlePosition1) OR (InMiddlePosition2) OR (InMiddlePosition3)) FOLD FORCE		
		WHEN (Others) RAISE FORCE
		// Early or middle positions, fold.
		// Others positions, raise.
	
	// ------------------------------------------------------------------------------------ //

	// ------------------------------------------------------------------------------------ //
	// Mixed hands.

	WHEN ((islist005))
		WHEN ((InEarlyPosition1) OR (InEarlyPosition2) OR (InEarlyPosition3)) FOLD FORCE
		WHEN ((InMiddlePosition1) OR (InMiddlePosition2) OR (InMiddlePosition3)) FOLD FORCE
		// Early or middle positions with hands like that, fold.

	WHEN ((islist005) AND (BotsActionsOnThisRoundIncludingChecks > 0))
		WHEN ((RaisesSinceLastPlay > 1)) FOLD FORCE
		WHEN (Others) CALL FORCE
	// More than one raise after me, fold.
	// One or no one raise after me, call. (If no raise check)

	WHEN ((islist005) AND (Raises > 0)) FOLD FORCE
	// Any raise before I play, fold.

	WHEN ((islist005) AND (Calls = 0)) RAISE FORCE
	// No calls before I play, raise.

	WHEN ((islist005) AND (Calls = 1))
		// Only one call, check my position:
		WHEN ((InSmallBlind) OR (InBigBlind)) CALL FORCE		
		WHEN (Others) FOLD FORCE
		// Small blind or big blind, call. (Check if is BB)
		// Otherwise fold.

	WHEN ((islist005) AND (Calls > 1)) CALL FORCE
	// More than one call, call.

	// ------------------------------------------------------------------------------------ //

    WHEN (Others) FOLD FORCE

FLOP

	// ------------------------------------------------------------------------------------ //
	// Made hands.

	// >>> MONSTER HANDS <<<
	WHEN ((HaveTwoPair) OR (HaveTrips) OR (HaveSet) OR (HaveStraight) OR ((HaveFlush) AND (SuitsInHand = 1)) OR (HaveFullHouse) OR (HaveQuads) OR (HaveStraightFlush))
		// Have two pair or better, look at the bets:
		WHEN ((BotsActionsOnThisRoundIncludingChecks = 0) AND (Bets = 1)) RAISE FORCE
		WHEN ((BotsActionsOnThisRoundIncludingChecks = 0) AND (Bets = 0)) BET FORCE
		WHEN ((BotsActionsOnThisRoundIncludingChecks > 0)) RAISE FORCE
		// It's my first play and someone bets, no care about re-raises, raise.
		// It's my first play and no bets, bet.
		// If I already played, raise.
	// >>> --- <<<

	// >>> TOP PAIR OR OVERPAIR <<<
	// This part was changed a litte bit from the original strategy.
	// It's just to keep security.
	WHEN ((HaveTopPair) OR (HaveOverPair))
		// Have top pair or over pair, look at the bets:
		WHEN ((BotsActionsOnThisRoundIncludingChecks = 0) AND (Bets = 1) AND (Raises = 0)) RAISE FORCE
		WHEN ((BotsActionsOnThisRoundIncludingChecks = 0) AND (Bets = 1) AND (Raises = 1)) CALL FORCE
		WHEN ((BotsActionsOnThisRoundIncludingChecks = 0) AND (Bets = 0)) BET FORCE
		WHEN ((BotsActionsOnThisRoundIncludingChecks > 0) AND (RaisesSinceLastPlay = 1)) CALL FORCE
		WHEN (Others) FOLD FORCE
		// It's my first play and one bet and no raise, raise.
		// It's my first play and one bet and one raise.
		// It's my first play and no bets, bet.
		// If I already played and is just one raise after me, call.
		// Otherwise fold.
	// >>> --- <<<

	// ------------------------------------------------------------------------------------ //

	// ------------------------------------------------------------------------------------ //
	// Draws

	// >>> MONSTER DRAW <<<
	WHEN ((HaveStraightDraw) AND (HaveFlushDraw) AND (SuitsInHand = 1))
		// We have a straight draw + flush draw. (Monster draw)
		// Look at the bets:
		WHEN ((BotsActionsOnThisRoundIncludingChecks = 0) AND (Bets = 1)) RAISE FORCE
		WHEN ((BotsActionsOnThisRoundIncludingChecks = 0) AND (Bets = 0)) BET FORCE
		WHEN ((BotsActionsOnThisRoundIncludingChecks > 0)) RAISE FORCE
		// Someone bets before I play, raise.
		// No bets before I play, bet.
		// Any decision after my first play will be raise.		
	// >>> --- <<<

	// >>> FLUSH DRAW, OESD OR DOUBLE GUTSHOT DRAW <<<
	WHEN (((HaveFlushDraw) AND (SuitsInHand = 1)) OR (HaveOpenEndedStraightDraw) OR (HaveDoubleGutshotDraw))
		// We have a flush draw or a OESD or a double gutshot draw.
		// Look at the bets:
		WHEN ((BotsActionsOnThisRoundIncludingChecks = 0) AND (Bets = 1)) RAISE FORCE
		WHEN ((BotsActionsOnThisRoundIncludingChecks = 0) AND (Bets = 0)) BET FORCE
		WHEN ((BotsActionsOnThisRoundIncludingChecks > 0)) CALL FORCE
		// Someone bets before I play, raise.
		// No bets before I play, bet.
		// Any decision after my first play will be call.
	// >>> --- <<<

	// >>> GUTSHOT DRAW AND OVERCARDS <<<
	WHEN ((HaveStraightDraw) AND (Overcards))
		// We have a gutshot draw and overcards.
		// Look at the bets:
		WHEN ((BotsActionsOnThisRoundIncludingChecks = 0) AND (Bets = 1)) RAISE FORCE
		WHEN ((BotsActionsOnThisRoundIncludingChecks = 0) AND (Bets = 0)) BET FORCE
		WHEN ((BotsActionsOnThisRoundIncludingChecks > 0)) CALL FORCE
		// Someone bets before I play, raise.
		// No bets before I play, bet.
		// Any decision after my first play will be call.
	// >>> --- <<<

	// >>> GUTSHOT DRAW OR OVERCARDS <<<
	// This part was changed a litte bit from the original strategy.
	// It's just to keep security.
	WHEN ((HaveStraightDraw) OR (Overcards))
		// We have a gutshot draw or overcards.
		// Look at the bets:
		WHEN ((BotsActionsOnThisRoundIncludingChecks = 0) AND (Raises > 0)) FOLD FORCE
		WHEN ((BotsActionsOnThisRoundIncludingChecks = 0) AND (Bets = 0) AND (BotRaisedBeforeFlop) AND (Opponents <= 2)) BET FORCE
		WHEN ((BotsActionsOnThisRoundIncludingChecks = 0) AND (Bets = 1) AND (PotSize >= 10)) CALL FORCE
		WHEN ((BotsActionsOnThisRoundIncludingChecks > 0) AND (RaisesSinceLastPlay = 1) AND (PotSize >= 10) AND (Opponents <= 2)) CALL FORCE
		WHEN (Others) FOLD FORCE
		// Someone raises before I play, fold.
		// That's my first play, no bets, I raised in preflop amd have two or less opponents, bet. (Continuation Bet)
		// That's my first play, one bet was made and the pot have at least 10 small bets, call.
		// If I already played, I call if: No more than one raise was made, the pot have at least 10 small bets amd have two or less opponents.
		// Otherwise fold.
	// >>> --- <<<
	
	// ------------------------------------------------------------------------------------ //

	// ------------------------------------------------------------------------------------ //
	// Trash Hands
	WHEN ((BotsActionsOnThisRoundIncludingChecks = 0) AND (Bets = 0) AND (BotRaisedBeforeFlop) AND (Opponents <= 2)) BET FORCE
	// We have a trash hand, that's my first play, no bets, we raised in preflop and have two or less opponents, bet. (Continuation Bet)
	// ------------------------------------------------------------------------------------ //

    WHEN (Others) FOLD FORCE

TURN

	// In turn and river the betsize is 1 big bet. (1 big bet = 2 big blinds)

	// ------------------------------------------------------------------------------------ //
	// That's my first play on turn.

	// >>> NO BETS AND WE HAVE THE INITIAVITE <<<
	WHEN ((BotsActionsOnThisRoundIncludingChecks = 0) AND (Bets = 0) AND (BotIsLastRaiser))
		WHEN ((HaveTopPair) OR (HaveOverPair) OR (HaveTwoPair) OR (HaveTrips) OR (HaveSet) OR (HaveStraight) OR ((HaveFlush) AND (SuitsInHand = 1)) OR (HaveFullHouse) OR (HaveQuads) OR (HaveStraightFlush)) BET FORCE
		WHEN ((Opponents <= 2) AND ((HaveStraightDraw) OR ((HaveFlushDraw) AND (SuitsInHand = 1)))) BET FORCE
		WHEN (Others) CALL FORCE
		// Top pair or better, bet.
		// Two or less opponents and we have a draw, bet.
		// Otherwise call. (Check)
	// >>> --- <<<

	// >>> NO BETS AND WE DON'T HAVE THE INITIAVITE <<<
	WHEN ((BotsActionsOnThisRoundIncludingChecks = 0) AND (Bets = 0) AND NOT (BotIsLastRaiser))
		WHEN ((HaveTwoPair) OR (HaveTrips) OR (HaveSet) OR (HaveStraight) OR ((HaveFlush) AND (SuitsInHand = 1)) OR (HaveFullHouse) OR (HaveQuads) OR (HaveStraightFlush)) BET FORCE
		WHEN (Others) CALL FORCE
		// Two pair or better, bet.
		// Otherwise call. (Check)
	// >>> --- <<<

	// >>> ONE BET BEFORE I PLAY (NO RAISES) <<<
	WHEN ((BotsActionsOnThisRoundIncludingChecks = 0) AND (Bets = 1) AND (Raises = 0))
		WHEN ((HaveTwoPair) OR (HaveTrips) OR (HaveSet) OR (HaveStraight) OR ((HaveFlush) AND (SuitsInHand = 1)) OR (HaveFullHouse) OR (HaveQuads) OR (HaveStraightFlush)) RAISE FORCE
		WHEN ((HaveTopPair) OR (HaveOverPair)) CALL FORCE
		WHEN (((HaveStraightDraw) AND (HaveFlushDraw) AND (SuitsInHand = 1)) OR ((HaveFlushDraw) AND (SuitsInHand = 1)) OR (HaveOpenEndedStraightDraw)) CALL FORCE
		WHEN ((HaveStraightDraw) AND (PotSize >= 20)) CALL FORCE
		WHEN (Others) FOLD FORCE
		// Two pair or better, bet.
		// Top pair or over pair, call.
		// Monster draws or flush draws or OESD, call.
		// Straight draw (gutshots) and the potsize is 10x higher than the bet size, call;
		// Otherwise fold.
		// Bet size is 1 big bet. (1 big bet = 2 big blinds).
		// (PotSize returns the value of pot in big blinds).		
	// >>> --- <<<

	// >>> ONE BET BEFORE I PLAY (WITH ONE OR MORE RAISES) <<<
	// This part was changed a litte bit from the original strategy.
	// It's just to keep security.
	WHEN ((BotsActionsOnThisRoundIncludingChecks = 0) AND (Bets = 1) AND (Raises >= 1))
		WHEN ((HaveTwoPair) OR (HaveTrips) OR (HaveSet) OR (HaveStraight) OR ((HaveFlush) AND (SuitsInHand = 1)) OR (HaveFullHouse) OR (HaveQuads) OR (HaveStraightFlush)) CALL FORCE
		WHEN (Others) FOLD FORCE
		// Note: The strategy says to raise in case of two pair or better and has no possibilities that opponents have a better hand.
		// But it also says that in case of two pair or better and has a possibility to opponent have a better hand, you should call.
		// I removed the "raise" condition.
		// So now its gonna "call" when have two pair or better. (With or without possibility to opponent have a better hand)
		// The name of this modification is: BSM1 (Big Security Measure 1)
	// >>> --- <<<

	// ------------------------------------------------------------------------------------ //

	// ------------------------------------------------------------------------------------ //
	// I already played on turn.

	// >>> ONE BET AFTER ME <<<
	WHEN ((BotsActionsOnThisRoundIncludingChecks > 0) AND (AmountToCall = 2))
		WHEN ((HaveTwoPair) OR (HaveTrips) OR (HaveSet) OR (HaveStraight) OR ((HaveFlush) AND (SuitsInHand = 1)) OR (HaveFullHouse) OR (HaveQuads) OR (HaveStraightFlush)) RAISE FORCE
		WHEN ((HaveTopPair) OR (HaveOverPair)) CALL FORCE
		WHEN (((HaveStraightDraw) AND (HaveFlushDraw) AND (SuitsInHand = 1)) OR ((HaveFlushDraw) AND (SuitsInHand = 1)) OR (HaveOpenEndedStraightDraw)) CALL FORCE
		WHEN ((HaveStraightDraw) AND (PotSize >= 20)) CALL FORCE
		WHEN (Others) FOLD FORCE
		// Two pair or better, raise.
		// Top pair or over pair, call.
		// Monster draws or flush draws or OESD, call.
		// Straight draw (gutshots) and the potsize is 10x higher than the bet size, call.
		// Otherwise fold.
	// >>> --- <<<

	// >>> MORE THAN ONE BET AFTER ME (ONE BET WITH ONE OR MORE RAISES) <<<
	// This part was changed a litte bit from the original strategy.
	// It's just to keep security.
	// (BSM1 Again)
	WHEN ((BotsActionsOnThisRoundIncludingChecks > 0) AND (AmountToCall > 2))
		WHEN ((HaveTwoPair) OR (HaveTrips) OR (HaveSet) OR (HaveStraight) OR ((HaveFlush) AND (SuitsInHand = 1)) OR (HaveFullHouse) OR (HaveQuads) OR (HaveStraightFlush)) CALL FORCE
		WHEN (((HaveStraightDraw) AND (HaveFlushDraw) AND (SuitsInHand = 1)) OR ((HaveFlushDraw) AND (SuitsInHand = 1))) CALL FORCE
		WHEN (((HaveOpenEndedStraightDraw) OR (HaveDoubleGutshotDraw)) AND NOT (FlushPossible)) CALL FORCE
		WHEN (Others) FOLD FORCE
		// Two pair or better, call.
		// Monster draws or flush draws, call.
		// If we have a OESD or double gutshots draw and no flush is possible on board, call.
		// Otherwise fold.
	// >>> --- <<<

    // ------------------------------------------------------------------------------------ //

	WHEN (Others) FOLD FORCE

RIVER

	// River is a quite simple...
	// And a little different too, but it's just for security.
	// So the differents rules in this river will be the BSM2.	

	WHEN (NOT (HaveBestOverpairOrBetter)) FOLD FORCE
	// If we dont't have a over pair or better, fold. (Normal rule)
	// So let's assume that you have a good hand to play on the river.

	WHEN ((BotsActionsOnThisRoundIncludingChecks = 0) AND (Bets = 0) AND (BotIsLastRaiser)) BET FORCE
	// That's my first play, no bets and I have the initiative, bet. (Normal rule)

	WHEN ((BotsActionsOnThisRoundIncludingChecks = 0) AND (Bets = 1) AND (BotIsLastRaiser)) CALL FORCE
	// One or more bet (raises) before I play and I have the initiative, call.
	// Basically a BSM1, but its BSM2, don't ask me why.

	WHEN ((BotsActionsOnThisRound > 0) AND (BotIsLastRaiser)) CALL FORCE
	// Any bet/raise after me and I have the initiative, call.
	// (BSM2 Again)	

	WHEN ((BotsActionsOnThisRoundIncludingChecks = 0) AND (Bets = 0) AND NOT (BotIsLastRaiser))
		// That's my first play, no bets and I don't have the initiative, check my hands:
		WHEN ((HaveTwoPair) OR (HaveTrips) OR (HaveSet) OR (HaveStraight) OR ((HaveFlush) AND (SuitsInHand = 1)) OR (HaveFullHouse) OR (HaveQuads) OR (HaveStraightFlush)) BET FORCE
		WHEN (Others) CALL FORCE
		// Two pair or better, bet.
		// Otherwise call. (Check)

	WHEN ((BotsActionsOnThisRoundIncludingChecks = 0) AND (Bets = 1) AND NOT (BotIsLastRaiser))
		// That's my first play, habe at least one bet to pay and I don't have the initiative, check my hands:
		WHEN ((HaveTwoPair) OR (HaveTrips) OR (HaveSet) OR (HaveStraight) OR ((HaveFlush) AND (SuitsInHand = 1)) OR (HaveFullHouse) OR (HaveQuads) OR (HaveStraightFlush)) CALL FORCE
		WHEN (Others) FOLD FORCE
		// Two pair or better, call.
		// Otherwise fold.

	WHEN ((BotsActionsOnThisRoundIncludingChecks > 0) AND NOT (BotIsLastRaiser))
		// I already played and don't have the initiative, check my bets:
		WHEN ((BotsActionsOnThisRound > 0)) CALL FORCE
		WHEN (Others) FOLD FORCE
		// If I put money on river, call.
		// Otherwise fold.

    WHEN (Others) FOLD FORCE 

// END
